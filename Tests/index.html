<!doctype html>
<html>
  <head>
      <!--per ricaricare sempre la pagina da zero e non usare la cache-->
      <META HTTP-EQUIV=”Pragma” CONTENT=”no-cache”>
      <META HTTP-EQUIV=”Cache-Control” CONTENT=”no-cache”>
      <style>
        body {
            background: #dddddd;
        }
        #my_Canvas {
            margin: 100px;
            padding: 2px;
            background: #ffffff;
            border: thin inset #aaaaaa;
        }
    </style>
   </head>
<body>

<canvas width="1280" height="720" id="my_Canvas"></canvas>

<script id="sun-vertex-shader" type="x-shader/x-vertex">

    float PI = 3.141592653589793;
    float TWOPI = 6.283185307179587;
    
    attribute vec3 a_position;
    attribute vec2 a_texcoord;

    uniform mat4 u_projection;  // projection matrix
    uniform mat4 u_view;        // view matrix
    uniform mat4 u_world;       // movement matrix
    
    varying vec2 v_texcoord;


    void main(void) {
        vec4 surfaceWorldPosition = u_world * vec4(a_position, 1.);
        gl_Position = u_projection * u_view * surfaceWorldPosition;

        v_texcoord = a_texcoord;
    }
</script>

<script id="sun-fragment-shader" type="x-shader/x-fragment">
    precision mediump float; 
    
    // Passed in from the vertex shader.
    varying vec2 v_texcoord;

    // The texture.
    uniform sampler2D u_texture;

    void main(void) {
        vec4 tex = texture2D( u_texture, v_texcoord );
        gl_FragColor = vec4(tex.r, tex.g, tex.b, tex.a);
    }
</script>

<script id="planet-vertex-shader" type="x-shader/x-vertex">

    float PI = 3.141592653589793;
    float TWOPI = 6.283185307179587;
    
    attribute vec3 a_position;
    attribute vec2 a_texcoord;
    attribute vec3 a_normal;

    uniform mat4 u_projection;  // projection matrix
    uniform mat4 u_view;        // view matrix
    uniform mat4 u_world;       // movement matrix
    uniform vec3 u_lightWorldPosition;
    uniform mat4 u_worldInverseTransposeMatrix;
    
    varying vec2 v_texcoord;
    varying vec3 v_normal;
    varying vec3 v_surfaceToLight;
 

    void main(void) {
        vec4 surfaceWorldPosition = u_world * vec4(a_position, 1.);
        gl_Position = u_projection * u_view * surfaceWorldPosition;
        v_normal = mat3(u_worldInverseTransposeMatrix) * a_normal;

        v_texcoord = a_texcoord.xy*vec2(TWOPI, PI);

        v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition.xyz;
    }
</script>

<script id="planet-fragment-shader" type="x-shader/x-fragment">
    precision mediump float; 
    
    // Passed in from the vertex shader.
    varying vec3 v_normal;
    varying vec3 v_surfaceToLight;
    varying vec2 v_texcoord;

    // The texture.
    uniform samplerCube u_texture;


    vec3 getSphericalTexcoord() {
        float theta = v_texcoord.y;
        float phi = v_texcoord.x;
        vec3 unit = vec3(0,0,0);

        unit.x = sin(phi) * sin(theta) * -1.0;
        unit.y = cos(theta) * -1.0;
        unit.z = cos(phi) * sin(theta) * -1.0;
        return unit;
    }

    void main(void) {

        vec3 normal = normalize(v_normal);

        vec3 surfaceToLightDirection = normalize(v_surfaceToLight);

        float light = dot(normal, surfaceToLightDirection);

        gl_FragColor = textureCube(u_texture, getSphericalTexcoord());

        // Lets multiply just the color portion (not the alpha)
        // by the light
        gl_FragColor.rgb *= (light*1.1) + vec3(0.2,0.2,0.2);
        
        // Add dark blue ambient light
        gl_FragColor.rgb += vec3(0.01, 0, 0.05);
    }
</script>

<script id="skybox-vertex-shader" type="x-shader/x-vertex">
    attribute vec4 a_position;
    varying vec4 v_position;
    void main() {
      v_position = a_position;
      gl_Position = vec4(a_position.xy, 1, 1);
    }
</script>
<script id="skybox-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

uniform samplerCube u_skybox;
uniform mat4 u_viewDirectionProjectionInverse;

varying vec4 v_position;
void main() {
    vec4 t = u_viewDirectionProjectionInverse * v_position;
    gl_FragColor = textureCube(u_skybox, normalize(t.xyz / t.w));
}
</script>

<script id="ship-vertex-shader" type="x-shader/x-vertex">
    attribute vec4 a_position;
    attribute vec3 a_normal;
    attribute vec2 a_texcoord;
    attribute vec4 a_color;
  
    uniform mat4 u_projection;
    uniform mat4 u_view;
    uniform mat4 u_world;
    uniform vec3 u_viewWorldPosition;
  
    varying vec3 v_normal;
    varying vec3 v_surfaceToView;
    varying vec2 v_texcoord;
    varying vec4 v_color;
  
    void main() {
      vec4 worldPosition = u_world * a_position;
      gl_Position = u_projection * u_view * worldPosition;
      v_surfaceToView = u_viewWorldPosition - worldPosition.xyz;
      v_normal = mat3(u_world) * a_normal;
      v_texcoord = a_texcoord;
      v_color = a_color;
    }
  </script>
  <!-- fragment shader -->
  <script id="ship-fragment-shader" type="x-shader/x-fragment">
    precision highp float;
  
    varying vec3 v_normal;
    varying vec3 v_surfaceToView;
    varying vec2 v_texcoord;
    varying vec4 v_color;
  
    uniform vec3 diffuse;
    uniform vec3 ambient;
    uniform vec3 emissive;
    uniform vec3 specular;
    uniform float shininess;
    uniform float opacity;
    uniform vec3 u_lightDirection;
    uniform vec3 u_ambientLight;
    uniform sampler2D diffuseMap;
  
    void main () {
      vec3 normal = normalize(v_normal);
  
      vec3 surfaceToViewDirection = normalize(v_surfaceToView);
      vec3 halfVector = normalize(u_lightDirection + surfaceToViewDirection);
  
      float light = dot(u_lightDirection, normal); //* .5 + .5;
      float specularLight = clamp(dot(normal, halfVector), 0.0, 1.0);
  
      vec4 diffuseMapColor = texture2D(diffuseMap, v_texcoord);
      vec3 effectiveDiffuse = diffuse * diffuseMapColor.rgb * v_color.rgb;
      float effectiveOpacity = opacity * diffuseMapColor.a * v_color.a;
  
      gl_FragColor = vec4(
          emissive +
          ambient * u_ambientLight +
          effectiveDiffuse * light +
          specular * pow(specularLight, shininess),
          effectiveOpacity);
    }
  </script>

<script type="text/javascript" src="resources/webgl-utils.js"></script>
<script type="text/javascript" src="resources/jquery-3.6.0.js"></script>
<script type="text/javascript" src="resources/shapes_utils.js"></script>
<script type="text/javascript" src="resources/glm_utils.js"></script>
<script type="text/javascript" src="resources/mesh_utils.js"></script>
<script type="text/javascript" src="resources/load_mesh.js"></script>
<script type="text/javascript" src="resources/dat.gui.js"></script>
<script type="text/javascript" src="../TextureGen/simplex-noise.js"></script>
<script type="text/javascript" src="resources/texture_loader.js"></script>
<script type="text/javascript" src="resources/m4.js"></script>
<script type="text/javascript" src="../TextureGen/chroma.min.js"></script>
<script type="text/javascript" src="../TextureGen/TextureGenerator.js"></script>
<script type="text/javascript" src="main.js"></script>
</body>
</html>  
